package de.uni_freiburg.informatik.ultimate.lib.srparse;

import java.io.IOException;
import java.io.InputStream;
import java.io.Reader;
import java.math.BigDecimal;

import com.github.jhoenicke.javacup.runtime.Symbol;

import de.uni_freiburg.informatik.ultimate.boogie.ast.BinaryExpression;
import de.uni_freiburg.informatik.ultimate.boogie.ast.BooleanLiteral;
import de.uni_freiburg.informatik.ultimate.boogie.ast.Expression;
import de.uni_freiburg.informatik.ultimate.boogie.ast.IdentifierExpression;
import de.uni_freiburg.informatik.ultimate.boogie.ast.IntegerLiteral;
import de.uni_freiburg.informatik.ultimate.boogie.ast.RealLiteral;
import de.uni_freiburg.informatik.ultimate.boogie.ast.UnaryExpression;
import de.uni_freiburg.informatik.ultimate.boogie.type.BoogieType;
import de.uni_freiburg.informatik.ultimate.core.model.models.ILocation;
import de.uni_freiburg.informatik.ultimate.core.model.services.ILogger;
import de.uni_freiburg.informatik.ultimate.core.model.services.IUltimateServiceProvider;
import de.uni_freiburg.informatik.ultimate.lib.pea.BoogieBooleanExpressionDecision;
import de.uni_freiburg.informatik.ultimate.lib.pea.BooleanDecision;
import de.uni_freiburg.informatik.ultimate.lib.pea.CDD;
import de.uni_freiburg.informatik.ultimate.lib.srparse.pattern.BndDelayedResponsePatternUT;
import de.uni_freiburg.informatik.ultimate.lib.srparse.pattern.BndEntryConditionPattern;
import de.uni_freiburg.informatik.ultimate.lib.srparse.pattern.BndExistencePattern;
import de.uni_freiburg.informatik.ultimate.lib.srparse.pattern.BndInvariancePattern;
import de.uni_freiburg.informatik.ultimate.lib.srparse.pattern.BndPossResponsePattern;
import de.uni_freiburg.informatik.ultimate.lib.srparse.pattern.BndReccurrencePattern;
import de.uni_freiburg.informatik.ultimate.lib.srparse.pattern.BndResponsePatternTT;
import de.uni_freiburg.informatik.ultimate.lib.srparse.pattern.BndResponsePatternTU;
import de.uni_freiburg.informatik.ultimate.lib.srparse.pattern.BndResponsePatternUT;
import de.uni_freiburg.informatik.ultimate.lib.srparse.pattern.ConstrainedChainPattern;
import de.uni_freiburg.informatik.ultimate.lib.srparse.pattern.InitializationPattern;
import de.uni_freiburg.informatik.ultimate.lib.srparse.pattern.InstAbsPattern;
import de.uni_freiburg.informatik.ultimate.lib.srparse.pattern.InvariantPattern;
import de.uni_freiburg.informatik.ultimate.lib.srparse.pattern.MaxDurationPattern;
import de.uni_freiburg.informatik.ultimate.lib.srparse.pattern.MinDurationPattern;
import de.uni_freiburg.informatik.ultimate.lib.srparse.pattern.PatternBuilder;
import de.uni_freiburg.informatik.ultimate.lib.srparse.pattern.PatternType;
import de.uni_freiburg.informatik.ultimate.lib.srparse.pattern.PrecedenceChain12Pattern;
import de.uni_freiburg.informatik.ultimate.lib.srparse.pattern.PrecedenceChain21Pattern;
import de.uni_freiburg.informatik.ultimate.lib.srparse.pattern.PrecedencePattern;
import de.uni_freiburg.informatik.ultimate.lib.srparse.pattern.ResponseChain12Pattern;
import de.uni_freiburg.informatik.ultimate.lib.srparse.pattern.ResponseChain21Pattern;
import de.uni_freiburg.informatik.ultimate.lib.srparse.pattern.ResponsePattern;
import de.uni_freiburg.informatik.ultimate.lib.srparse.pattern.UniversalityPattern;
import de.uni_freiburg.informatik.ultimate.lib.srparse.pattern.TogglePattern;
import de.uni_freiburg.informatik.ultimate.lib.srparse.pattern.TogglePatternDelayed;


parser ReqParser;
option symbols = ReqSymbols;
option java15, compact_red, newpositions;

parser code {:

	private IUltimateServiceProvider mServices;
	private String mFilename;
	private ILogger mLogger;

	public ReqParser(final IUltimateServiceProvider services, final ILogger logger, final Reader reader,
			final String filename) throws IOException {
		this(new ReqLexer(reader));
		mServices = services;
		mLogger = logger;
		mFilename = filename;
	}

	public ReqParser(final IUltimateServiceProvider services, final ILogger logger, final InputStream stream,
			final String filename) throws IOException {
		this(new ReqLexer(stream));
		mServices = services;
		mLogger = logger;
		mFilename = filename;
	}

	@Override
	public void report_error(final String s, final Object sym) {
		String location;
		if (sym instanceof MySymbolFactory.LineColumnSymbol) {
			final MySymbolFactory.LineColumnSymbol bsym = (MySymbolFactory.LineColumnSymbol) sym;
			location = bsym.getLocation();
		} else if ((sym instanceof Symbol) && ((Symbol) sym).sym == ReqSymbols.EOF) {
			location = "EOF";
		} else {
			location = "UNKNOWN";
		}
		final String filename = mFilename == null ? "" : (mFilename + ":");
		mLogger.error(filename + location + ": " + s);
	}

	public void report_error(final String s) {
		report_error(s, cur_token);
	}

	@Override
	public void syntax_error(final Symbol sym) {
		report_error("Syntax Error", sym);
	}
:}

action code {:
    public ILocation getLocation(Symbol sBegin, Symbol sEnd) {
	    int startLine = sBegin.left;
	    int endLine = sEnd.right;
	    int startColumn = 0;
	    int endColumn = 0;
	    return new ReqLocation(parser.mFilename, startLine, endLine, startColumn, endColumn);
	}
:}

/*init with {: getScanner().init(); :} */
scan with {: return getScanner().next_token(); :}

/* Keywords */
terminal GLOBALLY, BEFORE, AFTER, BETWEEN, AND, UNTIL, INPUT, OUTPUT, INTERNAL, CONST;
terminal IT, IS, NEVER, ALWAYS, THE, CASE, THAT, INITIALLY;
terminal IF, HOLD, HOLDS, HELD, EVENTUALLY;
terminal TRANSITIONS, TO, STATES, IN, WHICH, OCCUR, AT, TWICE, AFTERWARDS;
terminal THEN, PREVIOUSLY, AS, WELL;
terminal WAS, PRECEDED, SUCCEEDED, BY, TOGGLES;
terminal WHERE, DOES, NOT;
terminal ONCE, BECOMES, SATISFIED, FOR, EVERY, LATER;
terminal MOST, LEAST, LESS, THAN;
terminal TIME, UNITS, SEC, USEC, MSEC;
terminal THERE, ONE, EXECUTION, SEQUENCE, SUCH;
/* Other Symbols */
terminal DOT, COMMA, IDSEP;
terminal LAND, LOR, LNOT, LIMPLIES;
terminal LPAR, RPAR, QUOTE;
terminal LE, GREATER, LTEQ, GTEQ, NEQ, EQ, PARTORDER;
terminal PLUS, MINUS;
terminal TIMES, DIVIDE, MOD;
terminal TRUE, FALSE;
terminal String ID;
terminal String TYPE;
terminal String REALNUMBER;
terminal String NUMBER;

/* Non-terminals */
non terminal String propid;
non terminal CDD expression, expr;
non terminal String atom, cid, duration;
non terminal PatternType property;
non terminal PatternBuilder pattern, opattern, rtpattern;
non terminal SrParseScope scope;
non terminal PatternType[] patternset;
non terminal Integer num;
non terminal boolean bool;
non terminal Expression exprImplies, exprImpliesNI, exprAnd, exprAndNI, exprOr, exprOrNI;
non terminal Expression expr1NI, expr2NI, expr3NI, expr5NI; 
non terminal Expression expr6NI, expr7NI, expr9NI;

precedence left LOR;
precedence left LAND;
precedence nonassoc LNOT;

/* The grammar */

patternset ::=
	property*:set {: RESULT = set; :}
	;

property ::= propid?:id scope:s COMMA? pattern:p DOT?
               {: p.setScope(s).setId(id); RESULT = p.build(); :}
            | error
               {: RESULT = null; :}
          	| INPUT cid:i IS ID:t
		  	   {: RESULT = new InitializationPattern(i,t,InitializationPattern.VariableCategory.IN); :}
		  	| OUTPUT cid:i IS ID:t
		  	   {: RESULT = new InitializationPattern(i,t,InitializationPattern.VariableCategory.OUT); :}
		 	| INTERNAL cid:i IS ID:t
		  	   {: RESULT = new InitializationPattern(i,t,InitializationPattern.VariableCategory.HIDDEN); :}
		 	| CONST cid:i IS NUMBER:v
		  	   {: RESULT = new InitializationPattern(i,"int",InitializationPattern.VariableCategory.CONST, new IntegerLiteral(getLocation(v$,v$),BoogieType.TYPE_INT, v)); :}
		 	| CONST cid:i IS REALNUMBER:v
		  	   {: RESULT = new InitializationPattern(i,"real",InitializationPattern.VariableCategory.CONST, new RealLiteral(getLocation(v$,v$),BoogieType.TYPE_REAL, v)); :}
		 	| CONST cid:i IS bool:v
		  	   {: RESULT = new InitializationPattern(i,"bool",InitializationPattern.VariableCategory.CONST, new BooleanLiteral(getLocation(v$,v$),BoogieType.TYPE_BOOL, v)); :}		  	   		  	   		  	   
           ;

propid ::= ID:id IDSEP
			{: RESULT = id; :}
		;

scope ::=
    GLOBALLY
      {: RESULT = new SrParseScopeGlob(); :}
  | BEFORE expression:p
      {: RESULT = new SrParseScopeBefore(p); :}
  | AFTER expression:p
      {: RESULT = new SrParseScopeAfter(p); :}
  | BETWEEN expression:p AND expression:q 
      {: RESULT = new SrParseScopeBetween(p,q); :}
  | AFTER expression:p UNTIL expression:q
      {: RESULT = new SrParseScopeAfterUntil(p,q); :}
  ;
  
pattern ::=	IT IS NEVER THE CASE THAT expression:p HOLDS
			{: RESULT = new PatternBuilder(); RESULT.setType(InstAbsPattern.class); RESULT.addCdd(p); :}
  		| IT IS ALWAYS THE CASE THAT expression:p HOLDS
			{: RESULT = new PatternBuilder(); RESULT.setType(UniversalityPattern.class); RESULT.addCdd(p); :}
  		| IT IS ALWAYS THE CASE THAT IF expression:p HOLDS COMMA? THEN expression:q HOLDS AS WELL
      		{: RESULT = new PatternBuilder(); RESULT.setType(InvariantPattern.class); RESULT.addCdd(q,p); :}
	  	| expression:p EVENTUALLY HOLDS
	      	{: RESULT = new PatternBuilder(); RESULT.setType(ResponsePattern.class); RESULT.addCdd(BoogieBooleanExpressionDecision.createTrue(),p); :}
		| TRANSITIONS TO STATES IN WHICH expression:p HOLDS OCCUR AT MOST TWICE
	      	{: RESULT = new PatternBuilder(); RESULT.setType(BndExistencePattern.class); RESULT.addCdd(p); :}
		| IT IS ALWAYS THE CASE THAT opattern:p 
			{: RESULT = p; :}
	  	| IT IS ALWAYS THE CASE THAT rtpattern:p 
	  		{: RESULT = p; :}
	  	;
  
opattern ::= IF expression:p HOLDS COMMA? THEN expression:q PREVIOUSLY HELD
      			{: RESULT = new PatternBuilder(); RESULT.setType(PrecedencePattern.class); RESULT.addCdd(q,p); :}
  			| IF expression:p HOLDS AND IS SUCCEEDED BY expression:q COMMA? THEN expression:r PREVIOUSLY HELD
				{: RESULT = new PatternBuilder(); RESULT.setType(PrecedenceChain12Pattern.class); RESULT.addCdd(r,q,p); :}
  			| IF expression:p HOLDS COMMA? THEN expression:q PREVIOUSLY HELD AND WAS PRECEDED BY expression:r
				{: RESULT = new PatternBuilder(); RESULT.setType(PrecedenceChain21Pattern.class); RESULT.addCdd(r,q,p); :}
  			| IF expression:p HOLDS COMMA? THEN expression:q EVENTUALLY HOLDS
				{: RESULT = new PatternBuilder(); RESULT.setType(ResponsePattern.class); RESULT.addCdd(q,p); :}
  			| IF expression:p HOLDS COMMA? THEN expression:q EVENTUALLY HOLDS AND IS SUCCEEDED BY expression:r
				{: RESULT = new PatternBuilder(); RESULT.setType(ResponseChain12Pattern.class); RESULT.addCdd(r,q,p); :}
  			| IF expression:p HOLDS AND IS SUCCEEDED BY expression:q COMMA? THEN expression:r EVENTUALLY HOLDS AFTER expression:s
				{: RESULT = new PatternBuilder(); RESULT.setType(ResponseChain21Pattern.class); RESULT.addCdd(s,r,q,p); :}
  			| IF expression:p HOLDS COMMA? THEN expression:q EVENTUALLY HOLDS AND IS SUCCEEDED BY expression:r WHERE expression:s DOES NOT HOLD BETWEEN expression:t AND expression:u
				{: RESULT = new PatternBuilder(); RESULT.setType(ConstrainedChainPattern.class); RESULT.addCdd(u,s,t,r,q,p); :}
			| IF expression:p HOLDS COMMA? THEN expression:s TOGGLES expression:t
      			{: RESULT = new PatternBuilder(); RESULT.setType(TogglePattern.class); RESULT.addCdd(p,s,t); :}
  			;
  
rtpattern ::= ONCE expression:p BECOMES SATISFIED COMMA? IT HOLDS FOR AT LEAST duration:d
				{: RESULT = new PatternBuilder().setType(MinDurationPattern.class).addCdd(p).addDuration(d); :}
  			| ONCE expression:p BECOMES SATISFIED COMMA? IT HOLDS FOR LESS THAN duration:d
				{: RESULT = new PatternBuilder().setType(MaxDurationPattern.class).addCdd(p).addDuration(d); :}
			| expression:p HOLDS AT LEAST EVERY duration:d
				{: RESULT = new PatternBuilder().setType(BndReccurrencePattern.class).addCdd(p).addDuration(d); :}
  			| IF expression:r HOLDS COMMA? THEN expression:s HOLDS AFTER AT MOST duration:c1
				{: RESULT = new PatternBuilder().setType(BndResponsePatternUT.class).addCdd(s,r).addDuration(c1); :}
  			| IF expression:r HOLDS FOR AT LEAST duration:c1 COMMA? THEN expression:s HOLDS AFTERWARDS FOR AT LEAST duration:c2
				{: RESULT = new PatternBuilder().setType(BndResponsePatternTT.class).addCdd(s,r).addDuration(c1,c2); :}				
  			| IF expression:r HOLDS FOR AT LEAST duration:c1 COMMA? THEN expression:s HOLDS AFTERWARDS
				{: RESULT = new PatternBuilder().setType(BndResponsePatternTU.class).addCdd(s,r).addDuration(c1); :}
  			| IF expression:r HOLDS COMMA? THEN expression:s HOLDS AFTER AT MOST duration:c1 FOR AT LEAST duration:c2
				{: RESULT = new PatternBuilder().setType(BndDelayedResponsePatternUT.class).addCdd(s,r).addDuration(c1,c2); :}					
  			| IF expression:p HOLDS COMMA? THEN expression:s HOLDS FOR AT LEAST duration:d
				{: RESULT = new PatternBuilder().setType(BndInvariancePattern.class).addCdd(s,p).addDuration(d); :}
			| IF expression:p HOLDS COMMA? THEN THERE IS AT LEAST ONE EXECUTION SEQUENCE SUCH THAT expression:q HOLDS AFTER AT MOST duration:d
				{: RESULT = new PatternBuilder().setType(BndPossResponsePattern.class).addCdd(q,p).addDuration(d); :}
  			| AFTER expression:p HOLDS FOR duration:d COMMA? THEN expression:q HOLDS
				{: RESULT = new PatternBuilder().setType(BndEntryConditionPattern.class).addCdd(q,p).addDuration(d); :}
			| IF expression:p HOLDS COMMA? THEN expression:s TOGGLES expression:t AT MOST duration:d LATER
      			{: RESULT = new PatternBuilder(); RESULT.setType(TogglePatternDelayed.class); RESULT.addCdd(p,s,t).addDuration(d); :}
  			;

expression ::= QUOTE expr:e QUOTE {: RESULT = e; :}
 			;
             
expr ::= atom:a {: RESULT = BooleanDecision.create(a); :}
       | expr1NI:e {: RESULT = BoogieBooleanExpressionDecision.create(e); :}       
       ;

expr1NI ::= expr2NI:e1 LIMPLIES exprImpliesNI:e2
           {:RESULT = new BinaryExpression(getLocation(e1$,e2$), BinaryExpression.Operator.LOGICIMPLIES, e1, e2);:}
        | expr2NI:e {:RESULT = e;:}
        ;
        
exprImpliesNI ::= expr2NI:e1 LIMPLIES exprImpliesNI:e2
            {:RESULT = new BinaryExpression(getLocation(e1$,e2$), BinaryExpression.Operator.LOGICIMPLIES, e1, e2);:}
          | expr2NI:e {:RESULT = e;:}
          ;
          
expr2NI ::= expr3NI:e1 LAND exprAndNI:e2
           {:RESULT = new BinaryExpression(getLocation(e1$,e2$), BinaryExpression.Operator.LOGICAND, e1, e2);:}
        | expr3NI:e1 LOR exprOrNI:e2
           {:RESULT = new BinaryExpression(getLocation(e1$,e2$), BinaryExpression.Operator.LOGICOR, e1, e2);:}
        | expr3NI:e {:RESULT = e;:}
        ;
        
exprAndNI ::= expr3NI:e1 LAND exprAndNI:e2
           {:RESULT = new BinaryExpression(getLocation(e1$,e2$), BinaryExpression.Operator.LOGICAND, e1, e2);:}
          | expr3NI:e {:RESULT = e;:}
          ;
          
exprOrNI ::= expr3NI:e1 LOR exprOrNI:e2
           {:RESULT = new BinaryExpression(getLocation(e1$,e2$), BinaryExpression.Operator.LOGICOR, e1, e2);:}
          | expr3NI:e {:RESULT = e;:}
		  ;
       

expr3NI ::= expr5NI:e1 LE expr5NI:e2
           {:RESULT = new BinaryExpression(getLocation(e1$,e2$), BinaryExpression.Operator.COMPLT, e1, e2);:}
        | expr5NI:e1 GREATER expr5NI:e2
           {:RESULT = new BinaryExpression(getLocation(e1$,e2$), BinaryExpression.Operator.COMPGT, e1, e2);:}
        | expr5NI:e1 LTEQ expr5NI:e2
           {:RESULT = new BinaryExpression(getLocation(e1$,e2$), BinaryExpression.Operator.COMPLEQ, e1, e2);:}
        | expr5NI:e1 GTEQ expr5NI:e2
           {:RESULT = new BinaryExpression(getLocation(e1$,e2$), BinaryExpression.Operator.COMPGEQ, e1, e2);:}
        | expr5NI:e1 EQ expr5NI:e2
           {:RESULT = new BinaryExpression(getLocation(e1$,e2$), BinaryExpression.Operator.COMPEQ, e1, e2);:}
        | expr5NI:e1 NEQ expr5NI:e2
           {:RESULT = new BinaryExpression(getLocation(e1$,e2$), BinaryExpression.Operator.COMPNEQ, e1, e2);:}
        | expr5NI:e {:RESULT = e;:}
       ;
       
expr5NI ::= expr5NI:e1 PLUS expr6NI:e2
           {:RESULT = new BinaryExpression(getLocation(e1$,e2$), BinaryExpression.Operator.ARITHPLUS, e1, e2);:}
        | expr5NI:e1 MINUS expr6NI:e2
           {:RESULT = new BinaryExpression(getLocation(e1$,e2$), BinaryExpression.Operator.ARITHMINUS, e1, e2);:}
        | expr6NI:e {:RESULT = e;:}
        ;
        
expr6NI ::= expr6NI:e1 TIMES expr7NI:e2
           {:RESULT = new BinaryExpression(getLocation(e1$,e2$), BinaryExpression.Operator.ARITHMUL, e1, e2);:}
        | expr6NI:e1 DIVIDE expr7NI:e2
           {:RESULT = new BinaryExpression(getLocation(e1$,e2$), BinaryExpression.Operator.ARITHDIV, e1, e2);:}
        | expr6NI:e1 MOD expr7NI:e2
           {:RESULT = new BinaryExpression(getLocation(e1$,e2$), BinaryExpression.Operator.ARITHMOD, e1, e2);:}
        | expr7NI:e {:RESULT = e;:}
        ;
expr7NI ::= LNOT expr7NI:e
           {:RESULT = new UnaryExpression(getLocation(e$,e$), UnaryExpression.Operator.LOGICNEG, e);:} 
		| MINUS expr7NI:e
           {:RESULT = new UnaryExpression(getLocation(e$,e$), UnaryExpression.Operator.ARITHNEGATIVE, e);:}
        | expr9NI:e {:RESULT = e;:}
        ;

expr9NI ::= REALNUMBER:n {: RESULT = new RealLiteral(getLocation(n$,n$), BoogieType.TYPE_REAL, n); :}
	    | NUMBER:n {: RESULT = new IntegerLiteral(getLocation(n$,n$), BoogieType.TYPE_INT, n); :}
     	| cid:i {: RESULT = new IdentifierExpression(getLocation(i$,i$), i); :}
	    | bool:b {:RESULT = new BooleanLiteral(getLocation(b$,b$),BoogieType.TYPE_BOOL,b);:}
	    | LPAR expr1NI:e RPAR {: RESULT = e; :}
	    ;
	
cid ::= ID
      | cid:c DOT ID:id {: RESULT= c + "." + id; :}
      ;

bool ::= TRUE {: RESULT = true; :}
		|FALSE {: RESULT = false; :}
		;
		
duration ::= num:n TIME UNITS {: RESULT = String.valueOf(n); :}
           | num:n USEC {: RESULT = String.valueOf(n); :}
           | num:n MSEC {: RESULT = String.valueOf(n*1000); :}
           | num:n SEC {: RESULT = String.valueOf(n*1000000); :}
           | cid:i TIME UNITS {: RESULT = i; :}
           | QUOTE cid:i QUOTE TIME UNITS {: RESULT = i; :}
           ;
           
num ::= NUMBER:n {: RESULT = Integer.parseInt(n); :}
	  | REALNUMBER:n {: RESULT = BigDecimal.valueOf(Double.parseDouble(n)).intValueExact(); :}
      | QUOTE NUMBER:n QUOTE {: RESULT = Integer.parseInt(n); :}
      | QUOTE REALNUMBER:n QUOTE {: RESULT = BigDecimal.valueOf(Double.parseDouble(n)).intValueExact(); :}
      ;